plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.15.0'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'org.tonybaloney.security'
version '1.29.0'

def ktor_version = "2.3.3"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.1.0"
    testImplementation 'net.bytebuddy:byte-buddy:1.14.5'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.14.5'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    // HAck to get build working. Try removing in future
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.10.0'

    // For API clients
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-serialization-gson:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

tasks.withType(Test).configureEach {
    jvmArgs = [
            '-Djava.awt.headless=true'
    ]
}

intellij {
    type = 'PC'
    plugins = ['python-ce', 'org.jetbrains.plugins.yaml']
    updateSinceUntilBuild = false
    version = project.properties["intellijVersion"] ?: "2023.2"
}

patchPluginXml {
    changeNotes = """
      <h2>1.29.0</h2>
      <ul>
        <li>Support for 2023.2 series of IDEs.</li>
      </ul>
      """
}

publishPlugin {
    token = intellijPublishToken
    // channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}
