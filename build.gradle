plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.0.0'
}

group 'org.tonybaloney.security'
version '1.12.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "net.bytebuddy:byte-buddy:1.10.5"
    testImplementation "net.bytebuddy:byte-buddy-agent:1.10.5"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type 'PC'
    plugins 'python-ce', 'yaml'
    updateSinceUntilBuild false
}
// Make the intellij version overridable on the command line to support multiple build versions..
intellij.version = project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : '201.5616-EAP-CANDIDATE-SNAPSHOT'


patchPluginXml {
    changeNotes """
      <h2>1.12.0</h2>
      <ul>
        <li>Added [DJG103] to look for use of the Django `extra()` API on a query set with quoted parameters.</li>
        <li>Extended [DJG102] to include quoted templated variable names, e.g. `'%(variable)s'`</li>
        <li>Added [DJG104] to inspect usage of the Django Expression APIs with dangerous templates vulnerable to SQL injection.</li>
        <li>Modified [TRY100] to only verify `Exception` and no exception specified.</li>
      </ul>
      """
}

publishPlugin {
    token intellijPublishToken
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}