plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.0.0'
}

group 'org.tonybaloney.security'
version '1.20.0'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}
def ktor_version = "1.3.2"

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "net.bytebuddy:byte-buddy:1.10.8"
    testImplementation "net.bytebuddy:byte-buddy-agent:1.10.8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.70"
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-gson:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type 'PC'
    plugins 'python-ce', 'yaml'
    updateSinceUntilBuild false
}
// Make the intellij version overridable on the command line to support multiple build versions..
intellij.version = project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : '2020.1'


patchPluginXml {
    changeNotes """
      <h2>1.19.0</h2>
      <ul>
        <li>Added (DJG300) and (DJG301) for inspection of Django authorization mixins in class-based views.</li>
        <li>Fixed #118 : Fixers don't work for Mako and Jinja2 in edge case (@koxudaxi)</li>
      </ul>
      """
}

publishPlugin {
    token intellijPublishToken
    // channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}