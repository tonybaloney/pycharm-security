plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.2.1'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'org.tonybaloney.security'
version '1.24.3'

def ktor_version = "1.6.5"
def kotlin_version = "1.5.31"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.1'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "net.bytebuddy:byte-buddy:1.12.0"
    testImplementation "net.bytebuddy:byte-buddy-agent:1.11.22"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-gson:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
// Make the intellij version overridable on the command line to support multiple build versions..
def intellijversion = project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : '2021.2'

intellij {
    type = 'PC'
    plugins = ['python-ce', 'yaml']
    updateSinceUntilBuild = false
    version = intellijversion
}

patchPluginXml {
    changeNotes = """
      <h2>1.24.3</h2>
      <ul>
        <li>Safety db update</li>
      </ul>
      """
}

publishPlugin {
    token = intellijPublishToken
    // channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}