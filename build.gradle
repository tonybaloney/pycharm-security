plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.0.0'
}

group 'org.tonybaloney.security'
version '1.13.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "net.bytebuddy:byte-buddy:1.10.8"
    testImplementation "net.bytebuddy:byte-buddy-agent:1.10.8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type 'PC'
    plugins 'python-ce', 'yaml'
    updateSinceUntilBuild false
}
// Make the intellij version overridable on the command line to support multiple build versions..
intellij.version = project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : '2019.3.3'


patchPluginXml {
    changeNotes """
      <h2>1.13.0</h2>
      <ul>
        <li>Extended the behaviour of [DJG102] to not raise a warning when the safe string input is a string literal</li>
        <li>Added support for <a href="https://snyk.io">snyk.io</a> as the vulnerability database. Snyk offers an up to date and indepth database of Python package issues.</li>
      </ul>
      """
}

publishPlugin {
    token intellijPublishToken
    channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}